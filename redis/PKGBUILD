# Maintainer: Echizen Ryoma <echizenryoma.zhang@gmail.com>

pkgname=redis
pkgver=4.0.11
pkgrel=1
pkgdesc="Advanced key-value store"
arch=('x86_64')
url="http://redis.io"
license=('BSD')
depends=()
makedepends=("patch" "mingw-w64-x86_64-jemalloc" "mingw-w64-x86_64-lua51")
backup=('etc/redis.conf'
        'etc/sentinel.conf')
source=("https://github.com/antirez/${pkgname}/archive/${pkgver}.tar.gz"
        'hiredis.Makefile.patch')
sha256sums=('7e4d275eec261d06497521b4caa4bb7d10fa47b3e43a50a1c737bb6034d7364c'
            '1f24be193298f3f50824baee607a905ff85faecfc0e4090480b64b21202a15eb')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}/deps/hiredis"
  patch -p0 -i "${srcdir}/hiredis.Makefile.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"  
  PREFIX=${pkgdir}/usr make install
  
  mkdir -p "${pkgdir}/etc"
  install -m644 "${srcdir}/${pkgname}-${pkgver}"/redis.conf "${pkgdir}/etc/"
  install -m644 "${srcdir}/${pkgname}-${pkgver}"/sentinel.conf "${pkgdir}/etc/"
}
